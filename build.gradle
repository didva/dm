group 'com.epam.trainings.spring'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'java'

repositories {
    mavenCentral()
}

sourceSets {
    integTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integTest/groovy')
        }
        resources.srcDirs = [file('src/integTest/resources')]
    }
    test {
        groovy.srcDir file('src/test/groovy')
        if(System.properties.'idea.active') {
            resources.srcDirs = [file('src/integTest/resources'), file('src/test/resources')]
        }
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.springframework:spring-context:4.2.4.RELEASE'
    compile 'org.springframework:spring-aop:4.2.4.RELEASE'
    compile 'org.springframework:spring-jdbc:4.2.4.RELEASE'
    compile 'org.springframework:spring-tx:4.2.4.RELEASE'
    compile 'com.h2database:h2:1.4.191'
    compile 'org.aspectj:aspectjrt:1.8.8'
    compile 'org.aspectj:aspectjweaver:1.8.8'
    testCompile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.8.4'
    testCompile 'org.springframework:spring-test:4.2.4.RELEASE'
}

check.dependsOn integTest
integTest.mustRunAfter test